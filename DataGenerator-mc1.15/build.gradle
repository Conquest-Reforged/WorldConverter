buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "3.+", changing: true
    }
}

apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "net.minecraftforge.gradle"
sourceCompatibility = targetCompatibility = "1.8"

configurations {
    shade
    compile.extendsFrom(shade)
}

dependencies {
    shade(project(":ConverterCore")) { transitive(false) }
    minecraft "net.minecraftforge:forge:1.15.1-30.0.35"
}

minecraft {
    mappings channel: "snapshot", version: "20190719-1.14.3"
    runs {
        client {
            workingDirectory project.file("run")
            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
            property "forge.logging.console.level", "debug"
            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("run")
            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
            property "forge.logging.console.level", "debug"
            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }
    }
}

task shadeJar(type: Copy) {
    includeEmptyDirs = false
    from { configurations.shade.collect { zipTree(it) } }
    include("**/*.class")
    destinationDir("$buildDir/classes/java/main" as File)
}

jar {
    dependsOn(shadeJar)

    manifest {
        attributes([
                "Specification-Title": "DataGenerator",
                "Specification-Vendor": "DataGenerator",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"DataGenerator",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}